Функция ПолучитьСтруктуруТаблицНаСервере(МассивИменМетаданных) Экспорт
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	
	// Таблица значений, содержащая таблицы указанного объекта
	Таблицы = ПолучитьСтруктуруХраненияБазыДанных(МассивИменМетаданных);
	
	// пропишем дополнительные имена таблиц
	Для Каждого ТекСтрока Из Таблицы Цикл
		Если ПустаяСтрока(ТекСтрока.ИмяТаблицы) Тогда
			// найдем имя таблицы с этими
			Отбор = Новый Структура("Метаданные, Назначение", ТекСтрока.Метаданные, "Основная");
			НайденныеСтроки = Таблицы.НайтиСтроки(Отбор);
			Для Каждого ТекНайденнаяСтрока Из НайденныеСтроки Цикл
				Если ЗначениеЗаполнено(ТекНайденнаяСтрока.ИмяТаблицы) Тогда
					ТекСтрока.ИмяТаблицы = ТекНайденнаяСтрока.ИмяТаблицы + "." + ТекСтрока.Назначение;
					
					Для Каждого ТекПоле Из ТекСтрока.Поля Цикл
						Если ПустаяСтрока(ТекПоле.ИмяПоля) Тогда
							ОтборПоля = Новый Структура("ИмяПоляХранения", ТекПоле.ИмяПоляХранения);
							НайденныеПоля = ТекНайденнаяСтрока.Поля.НайтиСтроки(ОтборПоля);
							Для Каждого ТекНайдПоле Из НайденныеПоля Цикл
								Если ЗначениеЗаполнено(ТекНайдПоле.ИмяПоля) Тогда
									ТекПоле.ИмяПоля = ТекНайдПоле.ИмяПоля;
									Прервать;
								КонецЕсли;
							КонецЦикла;
						// Поля
						// ИмяПоля
						// ИмяПоляХранения
						КонецЕсли;
					КонецЦикла;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Таблица Из Таблицы Цикл
		
		ТекстовыйДокумент.ДобавитьСтроку("");
		
		ТекстовыйДокумент.ДобавитьСтроку("Таблица: " + Таблица.ИмяТаблицы + 
										 ", Имя таблицы хранения: " + Таблица.ИмяТаблицыХранения + 
										 ?(ПустаяСтрока(Таблица.ИмяТаблицы), ", Метаданные: " + Таблица.Метаданные, "") + 
										 ", Назначение: " + Таблица.Назначение);
	
		ТекстовыйДокумент.ДобавитьСтроку("- поля: "); 
		ПоляТаблицы = Таблица.Поля;
		Для Каждого Поле Из ПоляТаблицы Цикл
			
		ТекстовыйДокумент.ДобавитьСтроку("   " + Поле.ИмяПоля + 
										 " (" + Поле.ИмяПоляХранения + ")");
			
		КонецЦикла;	
		
		ТекстовыйДокумент.ДобавитьСтроку("- индексы: "); 
		ИндексыТаблицы = Таблица.Индексы;
		Для Каждого Индекс Из ИндексыТаблицы Цикл
			
			ТекстовыйДокумент.ДобавитьСтроку("   " + Индекс.ИмяИндексаХранения);
			
			СтрокаИндекса = "";
			СтрокаХраненияИндекса = "";
			ПоляИндекса = Индекс.Поля;
			Для Каждого ПолеИндекса Из ПоляИндекса Цикл
				
				Если СтрокаИндекса <> "" Тогда
					СтрокаИндекса = СтрокаИндекса + " + ";
				КонецЕсли;	
				СтрокаИндекса = СтрокаИндекса + ПолеИндекса.ИмяПоля;
				
				Если СтрокаХраненияИндекса <> "" Тогда
					СтрокаХраненияИндекса = СтрокаХраненияИндекса + " + ";
				КонецЕсли;	
				СтрокаХраненияИндекса = СтрокаХраненияИндекса + ПолеИндекса.ИмяПоляХранения;
				
			КонецЦикла;	
			
			ТекстовыйДокумент.ДобавитьСтроку("      " + СтрокаИндекса + 
											 " (" + СтрокаХраненияИндекса + ")");
					
		КонецЦикла;	
		
	КонецЦикла;	
	
	Возврат ТекстовыйДокумент;
	
КонецФункции	


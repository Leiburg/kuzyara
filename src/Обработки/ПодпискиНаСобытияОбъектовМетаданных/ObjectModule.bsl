
Процедура ВывестиПодписки(ТабДок, СписокВыбора) Экспорт
	
	ПорядокСобытий = Новый Соответствие;
	ПорядокСобытий.Вставить("ОбработкаЗаполнения", 1);
	ПорядокСобытий.Вставить("ОбработкаПроверкиЗаполения", 2);
	ПорядокСобытий.Вставить("ПередЗаписью", 3);
	ПорядокСобытий.Вставить("ПриУстановкеНовогоНомера", 4);
	ПорядокСобытий.Вставить("ПриУстановкеНовогоКода", 4);
	ПорядокСобытий.Вставить("ОбработкаУдаленияПроведения", 5);
	ПорядокСобытий.Вставить("ПриЗаписи", 6);
	ПорядокСобытий.Вставить("ОбработкаПроведения", 7);

	ПорядокСобытий.Вставить("ПриКопировании", 0);
	ПорядокСобытий.Вставить("ПередУдалением", 0);

	ТаблицаПодписок = Новый ТаблицаЗначений;
	ТаблицаПодписок.Колонки.Добавить("Источник", Новый ОписаниеТипов("Строка"), "Источник");
	ТаблицаПодписок.Колонки.Добавить("Событие", Новый ОписаниеТипов("Строка"), "Событие");
	ТаблицаПодписок.Колонки.Добавить("ИмяПодписки", Новый ОписаниеТипов("Строка"), "Имя подписки");
	ТаблицаПодписок.Колонки.Добавить("Обработчик", Новый ОписаниеТипов("Строка"), "Обработчик");
	ТаблицаПодписок.Колонки.Добавить("НомерСобытия", Новый ОписаниеТипов("Число"), "№ события п/п");
	ТаблицаПодписок.Колонки.Добавить("НомерПодписки", Новый ОписаниеТипов("Число"), "№ подписки п/п");

	СтруктураМетаданных = Новый Соответствие;

	Кол = Метаданные.ПодпискиНаСобытия.Количество();
	Сч=0;
	Для Каждого Подписка Из Метаданные.ПодпискиНаСобытия Цикл
		Сч = Сч + 1;
		
		ПодпискаТипы = Подписка.Источник.Типы();
		Для Каждого ТипОбъекта из ПодпискаТипы Цикл
			МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипОбъекта);
			ИмяМетаданных = МетаданныеОбъекта.ПолноеИмя();
			
			Мета = Лев(ИмяМетаданных, Найти(ИмяМетаданных, ".") - 1);
			Кол = СтруктураМетаданных[Мета];
			СтруктураМетаданных.Вставить(Мета, ?(Кол = Неопределено, 1, Кол + 1));
			
			Если ЗначениеЗаполнено(ТипДанных) И Мета <> ТипДанных Тогда
				Продолжить;
			КонецЕсли;

			НоваяСтрока = ТаблицаПодписок.Добавить();
			НоваяСтрока.ИмяПодписки = Подписка.Имя;
			НоваяСтрока.Событие 	= Подписка.Событие;
			НоваяСтрока.Обработчик	= Подписка.Обработчик;
			НоваяСтрока.Источник 	= ИмяМетаданных;
			НоваяСтрока.НомерСобытия= ПорядокСобытий[Строка(Подписка.Событие)];
			НоваяСтрока.НомерПодписки= Сч;
			
		КонецЦикла;
		
	КонецЦикла;
	
	СписокВыбора.Очистить();
	Для Каждого КлючЗначение Из СтруктураМетаданных Цикл
		СписокВыбора.Добавить(КлючЗначение.Ключ, КлючЗначение.Ключ + " (" + КлючЗначение.Значение + ")");
	КонецЦикла;
	
	ВывестиСКД(ТаблицаПодписок, ТабДок);
КонецПроцедуры

Процедура ВывестиСКД(ТЗ, ТабДок)
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;  
	// - Если сделать так, как показано выше(рекомендуют на некоторых ресурсах), то при изменении настроек в режиме клиента
	// этих изменений Вы не увидите, потому что настройки всегда будут по умолчанию. Как правильно - вариант ниже
	//Настройки = КомпоновщикНастроек.ПолучитьНастройки(); 
 
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; 
 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);	
	
	ВнешнийНаборДанных = Новый Структура("ТЗ", ТЗ); 
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки); 
	
	ТабДок.Очистить();
	
 	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент; 
	ПроцессорВывода.УстановитьДокумент(ТабДок); 
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);	
	
КонецПроцедуры